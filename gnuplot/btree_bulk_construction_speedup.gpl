# we want to use umlauts
set encoding iso_8859_1

# the enhanced option allows format to typeset exponents without problems
set terminal postscript eps enhanced color

# the name of the output file
set output "btree_bulk_construction_speedup.ps"

# the title above the diagram
set title "Speedup of Btree Bulk Construction"

# justify the legends to the left;
# that 'left Left' is no typo, but leaving 'Left' out does not change much
set key top left Left
set key box linestyle -1
set key samplen 1

# the text along the vertical axis
# (the numeric values are the results of trial and error experiments)
set lmargin 6
set label "Speedup" at screen 0.015,0.5 center  rotate

# the text along the horizontal axis
# (the numeric values are the results of trial and error experiments)
set bmargin 4
set label "Inserted elements (k)" at screen 0.5,0.03 center

# we are using logarithmic scales
set logscale x 10
#set logscale y 10

# Grid lines
set grid ytics lc rgb "#bbbbbb" lw 1 lt 0
set grid xtics lc rgb "#bbbbbb" lw 1 lt 0

# the tics along the horizontal axis and the labels for them
#set xtics rotate 
#set xtics  1,1
set format x "10^{%L}"

# finally the plot command
plot [:][0:6]\
 '< join -j 4 ../timings/btree/insert_sequ_r0_s1 ../timings/btree/insert_p_4_r0_s1' using 1:($2/$25) title ' btree (parallel, p=4)' with linespoints pt 4 linetype 4,\
 '< join -j 4 ../timings/btree/insert_sequ_r0_s1 ../timings/btree/insert_p_2_r0_s1' using 1:($2/$25) title ' btree (parallel, p=2)' with linespoints pt 3 linetype 3,\
 '< join -j 4 ../timings/btree/insert_sequ_r0_s1 ../timings/btree/insert_p_1_r0_s1' using 1:($2/$25) title ' btree (parallel, p=1)' with linespoints pt 2 linetype 2
 #'< join -j 4 ../timings/btree/insert_sequ_r0_s1 ../timings/btree/insert_p_8_r0_s1' using 1:($2/$25) title ' btree (parallel, p=8)' with linespoints pt 8 linetype 8,\

		    
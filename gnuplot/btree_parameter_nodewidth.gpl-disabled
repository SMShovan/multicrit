# we want to use umlauts
set encoding iso_8859_1

# the enhanced option allows format to typeset exponents without problems
set terminal postscript eps enhanced color

# the name of the output file
set output "btree_parameter_nodewidth.ps"

# the title above the diagram
set title "Insertion of k=10000 elements into tree of size n=100 * k"

# justify the legends to the left;
# that 'left Left' is no typo, but leaving 'Left' out does not change much
set key top left Left
set key box linestyle -1
set key samplen 1

# the text along the vertical axis
# (the numeric values are the results of trial and error experiments)
set lmargin 6
set label "Runtime [ns] per inserted element" at screen 0.015,0.5 center  rotate

# the text along the horizontal axis
# (the numeric values are the results of trial and error experiments)
set bmargin 4
set label "Node width in number of cachelines (64 bytes)" at screen 0.5,0.03 center

# we are using logarithmic scales
set logscale x 2
#set logscale y 10

# Grid lines
set grid ytics lc rgb "#bbbbbb" lw 1 lt 0
set grid xtics lc rgb "#bbbbbb" lw 1 lt 0

# the tics along the horizontal axis and the labels for them
#set xtics rotate 
#set xtics  1,1
#set format x "10^{%L}"

# finally the plot command
plot [:][:] '../timings/btree/insert_sequ_nodewidth_label' using 1:($2*1000/$5) title 'sequ. Btree (key=label)' with linespoints pt 10 linetype 10,\
	    '../timings/btree/insert_sequ_nodewidth_int' using 1:($2*1000/$5) title 'sequ. Btree (key=int)' with linespoints pt 7 linetype 7,\
            '../timings/btree/insert_p_nodewidth_label' using 1:($2*1000/$5) title 'par. Btree (key=label, p=4)' with linespoints pt 11 linetype 11,\
	    '../timings/btree/insert_p_nodewidth_int' using 1:($2*1000/$5) title 'par. Btree (key=int, p=4)' with linespoints pt 4 linetype 4
